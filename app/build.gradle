apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    signingConfigs {
        playStoreConfig {
            Properties keystoreProps = new Properties()
            if (file('keystore.config').exists()) {
                keystoreProps.load(new FileInputStream(file('keystore.config')))
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storePassword keystoreProps['storePassword']
                storeFile file('prospector.keystore.jks')
            }
        }
    }

    compileSdkVersion 31
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "org.phenoapps.prospector"

        //link square api has minimum requirement of 21
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 9
        versionName "1.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {

        release {
            clean
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), "proguard-rules.pro"
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.playStoreConfig //Add your own signing config
            pseudoLocalesEnabled false
            zipAlignEnabled true
        }

        debug {
            clean
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"

    productFlavors {
        demo {
            dimension "version"
            versionNameSuffix "-demo"
        }

        full {
            dimension "version"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

kapt {
    correctErrorTypes true
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:27.1.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'

    kapt 'com.android.databinding:compiler:3.1.4'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation group: 'org.apache.commons', name: 'commons-math', version: '2.2'

    // Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation "androidx.navigation:navigation-runtime-ktx:$nav_version"

    // Lifecycle (currently latest stable: 2.2.0)
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    //Room
    kapt "androidx.room:room-compiler:$room_lifecycle_version"
    implementation "androidx.room:room-ktx:$room_lifecycle_version"
    implementation "androidx.room:room-runtime:$room_lifecycle_version"

    //about fragment
    //github repositories
    implementation "com.github.MFlisar:changelog:$changelog_version"
    implementation "com.mikepenz:aboutlibraries-core:$about_version"
    implementation "com.mikepenz:aboutlibraries:$about_version"
    implementation "com.github.daniel-stoneuk:material-about-library:$daniel_version"

    //apache
    implementation "org.apache.commons:commons-csv:1.5"

    //androidx
    //noinspection GradleDependency
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.recyclerview:recyclerview:$recycler_version"
    //noinspection GradleDependency
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    //noinspection GradleDependency
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    //noinspection GradleDependency
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.legacy:legacy-support-core-utils:$legacy_version"
    implementation "androidx.collection:collection-ktx:$collection_version"
    //noinspection GradleDependency
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.legacy:legacy-support-v13:$legacy_version"
    implementation "androidx.vectordrawable:vectordrawable:$vectordrawable_version"
    implementation "androidx.multidex:multidex:$multidex_version"

    //material components
    //noinspection GradleDependency
    implementation "com.google.android.material:material:$material_version"

    //other
    implementation "com.journeyapps:zxing-android-embedded:$journeyapps_version"
//    implementation "com.jjoe64:graphview:$graphview_version"
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    //app intro github AndroidX Capable version
    implementation 'com.github.AppIntro:AppIntro:6.1.0'

    //local
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    implementation project(path: ':PhenoIcons')
}

